// Copyright 2000-2021 JetBrains s.r.o. Use of this source code is governed by the Apache 2.0 license that can be found in the LICENSE file.
apply plugin: 'java'
apply plugin: 'maven-publish'

compileJava {
  sourceCompatibility '17'
  targetCompatibility '17'
}

group = 'net.fabricmc'
archivesBaseName = 'fabric-fernflower'

version = '1.5.0'

def ENV = System.getenv()
version = version + (ENV.GITHUB_ACTIONS ? "" : "+local")

sourceSets {
  main.java.srcDirs 'src'
  test.java.srcDirs 'test'
}

repositories { mavenCentral() }
dependencies {
  implementation 'org.jetbrains:annotations:20.1.0'
  testImplementation 'junit:junit:4.12'
  testImplementation 'org.assertj:assertj-core:3.23.1'
}

jar {
  manifest {
    attributes 'Main-Class': 'org.jetbrains.java.decompiler.main.decompiler.ConsoleDecompiler'
  }
}

task sourceJar(type:Jar) {
  classifier = "sources"
  from sourceSets.main.allSource
}

tasks.withType(JavaCompile).configureEach {
  it.options.encoding = "UTF-8"

  it.options.release = 11
}

java {
  sourceCompatibility = JavaVersion.toVersion(11)
  targetCompatibility = JavaVersion.toVersion(11)
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java
      artifact(sourceJar)
    }
  }

  // select the repositories you want to publish to
  repositories {
    if (ENV.MAVEN_URL) {
      maven {
        url ENV.MAVEN_URL
        credentials {
          username ENV.MAVEN_USERNAME
          password ENV.MAVEN_PASSWORD
        }
      }
    }
  }
}

// Dont publish module metadata to prevent gradle resolving this dep on outdated java versions.
tasks.withType(GenerateModuleMetadata) {
  enabled = false
}
